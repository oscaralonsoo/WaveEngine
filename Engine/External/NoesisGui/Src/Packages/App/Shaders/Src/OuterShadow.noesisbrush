#include "BrushHelpers.h"

uniforms
{
    fixed4 _color;
    float _width;
    float _height;
    float _cornerR;
    float _blurR;
};

// Blurred rounded rectangles
// https://raphlinus.github.io/graphics/2020/04/21/blurred-rounded-rects.html

float erf7(float x)
{
    x *= 1.12837916709551257389615890312154517; // 2 / sqrt(PI)
    float xx = x * x;
    x += (0.24295 + (0.03395 + 0.0104 * xx) * xx) * (x * xx);
    return x / sqrt(1.0 + x * x);
}

float blurredRoundBox(float2 uv, float w, float h, float r, float s)
{
    float s_inv = 1.0 / s;

    float min_edge = min(w, h);
    float rmax = 0.5 * min_edge;
    float r0 = min(length(float2(r, s * 1.15)), rmax);
    float r1 = min(length(float2(r, s * 2.0)), rmax);

    float exponent = 2.0 * r1 / r0;
    float recip_exponent = 1.0 / exponent;

    // Pull in long end (make less eccentric).
    float delta = 1.25 * s * (exp(-(0.5 * s_inv * w) * (0.5 * s_inv * w)) - exp(-(0.5 * s_inv * h) * (0.5 * s_inv * h)));
    w = w + min(delta, 0.0);
    h = h - max(delta, 0.0);
    
    float scale = 0.5 * erf7(s_inv * 0.5 * (max(w, h) - 0.5 * r));

    float2 xy0 = abs(uv) - (float2(w,h) * 0.5 - r1);
    float2 xy1 = max(xy0, 0.0);

    float d_pos = pow(pow(xy1.x, exponent) + pow(xy1.y, exponent), recip_exponent);
    float d_neg = min(0.0, max(xy0.x, xy0.y));

    float d = d_pos + d_neg - r1;
    float z = scale * (erf7(s_inv * (min_edge + d)) - erf7(s_inv * d));

    return z;
}

fixed4 main_brush(float2 uv)
{
    float sigma = 1.0 + 0.42 *_blurR;
    float width = _width - _blurR * 2.0;
    float height = _height - _blurR * 2.0;
    float2 pos = (uv + float2(-0.5, -0.5)) * float2(_width, _height);

    float a = blurredRoundBox(pos, width, height, _cornerR, sigma);
    return float4(_color.r * a, _color.g * a, _color.b * a, a);
}
